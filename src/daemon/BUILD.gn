import("//build/template/protobuf.gni")

executable("clangd") {
  sources = [
    "clangd_main.cc",
  ]

  deps += [
    ":configuration",
    ":daemon",
    "//src/base:base",
    "//src/base:logging",
  ]
}

# Separate source set for unit-tests.
source_set("daemon") {
  visibility += [ "//src/test:unit_tests" ]

  sources = [
    "absorber.cc",
    "absorber.h",
    "base_daemon.cc",
    "base_daemon.h",
    "collector.cc",
    "collector.h",
    "compilation_daemon.cc",
    "compilation_daemon.h",
    "emitter.cc",
    "emitter.h",
  ]

  deps += [
    ":remote_proto",
    "//src/base:base",
    "//src/base:logging",
    "//src/cache:file_cache",
    "//src/net:net",
    "//src/perf:counter",
    "//src/perf:stat_service",
  ]

  public_deps = [
    ":configuration",
  ]
}

source_set("configuration") {
  sources = [
    "configuration.cc",
    "configuration.h",
  ]

  configs += [ "//build/config:version" ]

  deps += [
    ":config_proto",
    "//src/base:base",
    "//src/base:logging",
    "//src/third_party/gflags:gflags",
  ]
}

protobuf("config_proto") {
  sources = [
    "configuration.proto",
  ]

  deps = [
    "//src/base:base_proto",
  ]
}

protobuf("remote_proto") {
  deps = [
    "//src/net:net_proto",
  ]
  sources = [
    "remote.proto",
  ]
}
