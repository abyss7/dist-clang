import("//build/template/protobuf.gni")

executable("clang") {
  sources = [
    "clang_main.cc",
  ]

  deps += [
    ":client",
    ":config_proto",
    "//src/base:base",
    "//src/base:logging",
    "//src/perf:stat_service",
    "//src/perf:counter",
  ]
}

# Separate source set for unit-tests.
source_set("client") {
  visibility += [ "//src/test:unit_tests" ]

  sources = [
    "clang.cc",
    "clang.h",
  ]

  deps += [
    ":command",
    "//src/base:base",
    "//src/base:logging",
    "//src/net:net",
    "//src/net:net_proto",
  ]
}

# NOTICE: Uses custom libclang ldflags, thus, have to be shared library.
shared_library("command") {
  visibility = []
  visibility = [
    ":*",
    "//:*",
    "//src/test:unit_tests",
  ]

  sources = [
    "command.cc",
    "command.h",
  ]

  configs += [ "//build/config:libclang" ]

  deps += [
    "//src/base:base",
    "//src/base:logging",
  ]

  public_configs = [ "//build/config:libclang_includes" ]
}

protobuf("config_proto") {
  sources = [
    "configuration.proto",
  ]
}
