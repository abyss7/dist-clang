import("//build/template/protobuf.gni")

source_set("sqlite") {
  visibility = []
  visibility = [ ":*" ]

  sources = [
    "sqlite3.c",
    "sqlite3.h",
  ]

  cflags = [ "-Wno-incompatible-pointer-types-discards-qualifiers" ]
}

shared_library("file_cache") {
  visibility = []
  visibility = [
    "//:*",
    "//src/daemon:*",
    "//src/test:unit_tests",
  ]

  sources = [
    "database.h",
    "database_leveldb.cc",
    "database_leveldb.h",
    "database_sqlite.cc",
    "database_sqlite.h",
    "database_sqlite_migrator.cc",
    "file_cache.cc",
    "file_cache.h",
    "file_cache_migrator.cc",
  ]

  public = [ "file_cache.h" ]

  configs += [ "//build/config:libclang_includes" ]

  # FIXME: make the libclang a shared library.
  if (!config_for_tests || host_os == "mac") {
    configs += [ "//build/config:libclang" ]
  }

  deps += [
    ":sqlite",
    "//src/base:base",
    "//src/base:logging",
    "//src/perf:stat_service",
    "//src/third_party/leveldb:leveldb",
    "//src/third_party/snappy:snappy",
  ]

  public_deps = [ ":manifest_proto" ] # for file_cache_migrator_test.cc
}

protobuf("manifest_proto") {
  sources = [
    "manifest.proto",
  ]
}
